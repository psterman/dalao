plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'  // 重新启用KAPT
    id 'kotlin-parcelize'
}

android {
    namespace 'com.example.aifloatingball'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.aifloatingball"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        ndk {
            // 针对国产手机环境优化：仅保留主流架构
            // 绝大多数现代安卓设备为 arm64-v8a，保留 armeabi-v7a 以兼容旧设备
            // 移除 x86/x86_64 (模拟器用)，减小APK体积
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }
    
    buildTypes {
        release {
            minifyEnabled true  // 启用代码压缩，减少APK大小30-50%
            shrinkResources true  // 启用资源压缩，移除未使用的资源
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // 启用资源压缩时可能会移除一些动态加载的资源，需要keep这些资源
            resValue "string", "app_name", "AI助手"
        }
        debug {
            debuggable false  // 禁用调试模式，避免"Waiting For Debugger"问题
            minifyEnabled false  // Debug版本不压缩，便于调试
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        encoding = 'UTF-8'
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    
    packaging {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/*.kotlin_module',
                // 排除重复的 JNA 许可证文件
                'META-INF/AL2.0',
                'META-INF/LGPL2.1'
            ]
            // 解决重复类冲突：优先使用第一个找到的文件
            pickFirsts += [
                'META-INF/AL2.0',
                'META-INF/LGPL2.1',
                // 确保原生库文件被正确包含（JNA 和 Vosk 都需要）
                '**/libjnidispatch.so',
                '**/libvosk.so',
                // 对于 JNA 的重复类，优先使用 AAR 版本（包含原生库）
                // 这可以解决 @aar 格式同时引入 AAR 和 JAR 导致的重复类问题
                'com/sun/jna/**'
            ]
        }
    }
}

// 解决 JNA 重复类问题
// 问题：使用 @aar 格式时，Gradle 可能同时引入 AAR 和 JAR 版本
// 解决方案：在所有配置中排除 JAR 格式的 JNA，只保留我们显式添加的 AAR 版本
configurations.all {
    resolutionStrategy {
        // 强制使用统一版本的 JNA
        force 'net.java.dev.jna:jna:5.16.0'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.webkit:webkit:1.8.0'
    implementation 'androidx.annotation:annotation:1.7.1'
    
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    
    implementation 'androidx.dynamicanimation:dynamicanimation:1.1.0-alpha03'
    implementation 'androidx.window:window:1.2.0'
    
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-service:2.7.0'
    
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.belerweb:pinyin4j:2.5.1'
    implementation 'com.airbnb.android:lottie:6.1.0'
    
    // Markdown解析和渲染
    implementation 'io.noties.markwon:core:4.6.2'
    implementation 'io.noties.markwon:html:4.6.2'
    implementation 'io.noties.markwon:image:4.6.2'
    implementation 'io.noties.markwon:linkify:4.6.2'
    implementation 'io.noties.markwon:ext-tables:4.6.2'
    implementation 'io.noties.markwon:ext-strikethrough:4.6.2'
    implementation 'io.noties.markwon:ext-tasklist:4.6.2'
    
    // 解决依赖冲突
    implementation('org.jetbrains:annotations:23.0.0') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    
    implementation "androidx.interpolator:interpolator:1.0.0"

    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'

    // OkHttp for network requests
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // WorkManager for background tasks
    implementation 'androidx.work:work-runtime-ktx:2.9.0'

    // ML Kit Text Recognition
    implementation 'com.google.mlkit:text-recognition:16.0.0'
    implementation 'com.google.mlkit:text-recognition-chinese:16.0.0'
    implementation 'com.google.mlkit:text-recognition-devanagari:16.0.0'
    implementation 'com.google.mlkit:text-recognition-japanese:16.0.0'
    implementation 'com.google.mlkit:text-recognition-korean:16.0.0'

    // Tesseract OCR (for future implementation)
    implementation 'com.rmtheis:tess-two:9.1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.1.1'
    testImplementation 'org.mockito:mockito-inline:5.1.1'  // 支持final类和方法的mock
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Kotlin协程
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // Room数据库
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"  // 恢复kapt


    // Lottie for animations
    def lottieVersion = "5.2.0"
    implementation "com.airbnb.android:lottie:$lottieVersion"
    
    // ExoPlayer - 视频播放器（替代 VideoView）
    def media3_version = "1.2.0"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    implementation "androidx.media3:media3-common:$media3_version"
    // DASH 流媒体支持
    implementation "androidx.media3:media3-exoplayer-dash:$media3_version"
    // HLS 流媒体支持
    implementation "androidx.media3:media3-exoplayer-hls:$media3_version"
    // SmoothStreaming 支持
    implementation "androidx.media3:media3-exoplayer-smoothstreaming:$media3_version"
    
    // Vosk 离线语音识别库
    implementation('com.alphacephei:vosk-android:0.3.45') {
        // 排除 Vosk 可能传递依赖的 JNA，避免与显式添加的 AAR 版本冲突
        exclude group: 'net.java.dev.jna', module: 'jna'
    }
    
    // JNA (Java Native Access) - Vosk 依赖的原生库支持
    // 使用 @aar 格式确保包含 Android 原生库文件
    // 版本 5.16.0 修复了 16KB 页大小设备的兼容性问题
    // 注意：如果仍然出现重复类错误，可能需要手动排除 JAR 格式
    // 可以通过运行 ./gradlew :app:dependencies 查看依赖树
    implementation('net.java.dev.jna:jna:5.16.0@aar')
    
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:-options")
    options.encoding = 'UTF-8'
    options.forkOptions.jvmArgs.add('-Xmx2048m')
}

tasks.register('cleanJdkImageCache', Delete) {
    delete fileTree(dir: "${System.getProperty('user.home')}/.gradle/caches/transforms-3") {
        include "**/jdkImage/**"
        include "**/temp/jmod/**"
    }
}
